buildscript {
    dependencies {
        classpath 'org.flywaydb:flyway-mysql:11.17.0'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.25'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.25'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
    id "nu.studer.jooq" version "9.0"
    id "org.flywaydb.flyway" version "11.17.0"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'auth'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-data-redis")


    implementation("com.mysql:mysql-connector-j")       // MySQL JDBC
    runtimeOnly("io.jsonwebtoken:jjwt-impl:0.12.6")
    runtimeOnly("io.jsonwebtoken:jjwt-jackson:0.12.6")

    implementation("io.jsonwebtoken:jjwt-api:0.12.6")
    implementation("org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0")
    implementation("com.bucket4j:bucket4j-core:8.7.0")
    implementation("com.bucket4j:bucket4j-redis:8.7.0")

    implementation("org.springframework.boot:spring-boot-starter-jooq")
    jooqGenerator("com.mysql:mysql-connector-j")     // JOOQ MySQL generator

    implementation("org.flywaydb:flyway-core")       // Flyway core
    implementation("org.flywaydb:flyway-mysql")      // Flyway MySQL plugin

    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testImplementation("org.springframework.security:spring-security-test")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

def dbUrl = project.findProperty("db.url")?.toString()
def dbUser = project.findProperty("db.username")?.toString()
def dbPassword = project.findProperty("db.password")?.toString()
def dbSchema = project.findProperty("db.schema")?.toString()

flyway {
    url = dbUrl
    user = dbUser
    password = dbPassword
    schemas = [dbSchema]
    locations = ['filesystem:src/main/resources/db/migration']
    baselineOnMigrate = true
}

jooq {
    version = dependencyManagement.importedProperties['jooq.version']

    configurations {
        main {
            generateSchemaSourceOnCompilation = true

            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN

                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = dbUrl
                    user = dbUser
                    password = dbPassword
                }

                generator {
                    name = 'org.jooq.codegen.KotlinGenerator'

                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = dbSchema
                        excludes = 'flyway_schema_history'
                    }

                    target {
                        packageName = 'com.example.auth.generated.jooq'
                        directory = 'build/generated-src/jooq/main'
                    }

                    generate {
                        daos = true
                        pojos = true
                        pojosAsKotlinDataClasses = true
                        fluentSetters = true
                    }
                }
            }
        }
    }

}

kotlin {
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}